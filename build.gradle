buildscript {
    ext {}
    repositories {
        maven {
            url "https://artifactory.exclamationlabs.com/artifactory/libs-release-local"
        }
    }
    dependencies {
        classpath(group: 'com.exclamationlabs.connid', name: 'connector-base-config-plugin', version: "${config_plugin_version}-+")
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version "${artifactory_publish_version}"
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'localTemp'
    }

    maven {
        url "https://artifactory.exclamationlabs.com/artifactory/libs-release-local"
    }

    maven {
        url "https://nexus.evolveum.com/nexus/content/repositories/releases/"
    }
}

apply plugin: 'com.exclamationlabs.connid.base.config.plugin'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def build_version = System.getenv('BUILD_NUMBER') == null ? 'SNAPSHOT' : System.getenv('BUILD_NUMBER')
project.version="${project_version}-${build_version}"

def versionNumber = "${project_version}"

dependencies {

    // Required for MidPoint runtime
    runtimeOnly 'org.codehaus.groovy:groovy:2.5.2'
    runtimeOnly 'com.evolveum.polygon:connector-common:1.5.0.0'

    // HTTP/RESTful support
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.apache.httpcomponents:httpclient:4.5.10'

    implementation "com.exclamationlabs.connid:connector-base:${base_connector_version}-+"

    testImplementation "com.exclamationlabs.connid:connector-base-test-support:${base_connector_version}-+"

    testImplementation 'net.tirasa.connid:connector-framework-internal:1.5.0.0'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'generatedConfig']
        }
    }
}

task copyDependencyJars(type: Copy) {
    from configurations.runtimeClasspath.collect {
        if(it.isFile() && it.getCanonicalFile().getCanonicalPath().endsWith('.jar') &&
                (!it.getCanonicalFile().getCanonicalPath().contains("connector-framework"))) {
            it
        }
    }
    into file("custom/lib")
}

task baseClean(type: Delete) {
    delete 'custom'
    delete 'generatedConfig'
}


def fatClassifierName = 'fat'
def thinClassifierName = 'thin'

task fatJar(type: Jar) {

    archiveClassifier = fatClassifierName
    archiveName("${project.name}-${project.version}-${fatClassifierName}.jar")
    from 'custom', 'build/classes/java/main', 'src/main/resources'

    manifest {
        attributes("ConnectorBundle-FrameworkVersion": "1.4.3.11",
                "ConnectorBundle-Version": versionNumber,
                "ConnectorBundle-Name": project.name)
    }
}

task thinJar(type: Jar) {

    archiveClassifier = thinClassifierName
    archiveName("${project.name}-${project.version}-${thinClassifierName}.jar")
    from 'build/classes/java/main'

}

thinJar.dependsOn('compileJava')

fatJar.dependsOn('compileJava', 'copyDependencyJars')
compileJava.dependsOn('generateConfiguration')

clean.dependsOn('baseClean')


publishing {
    publications {
        connector_fat(MavenPublication) {
            artifact fatJar
        }

        connector_thin(MavenPublication) {
            artifact thinJar
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.connector_fat, publishing.publications.connector_thin)
}

artifactory {
    contextUrl = 'https://artifactory.exclamationlabs.com/artifactory'
    publish {
        repository {
            repoKey = System.getenv('ARTIFACTORY_GRADLE_REPO')
            username = System.getenv('ARTIFACTORY_GRADLE_USER')
            password = System.getenv('ARTIFACTORY_GRADLE_PASS')
        }
        defaults {
            publications('connector_fat', 'connector_thin')
            publishArtifacts = true

            publishPom = true // Publish generated POM files to Artifactory (true by default)
            publishIvy = false // Publish generated Ivy descriptor files to Artifactory (true by default)
        }
    }
}